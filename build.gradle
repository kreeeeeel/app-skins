plugins {
  id 'java'
  id 'application'
  id 'org.jetbrains.kotlin.jvm' version '1.8.22'
  id 'org.javamodularity.moduleplugin' version '1.8.12'
  id 'org.openjfx.javafxplugin' version '0.0.13'
  id 'org.beryx.jlink' version '2.25.0'
}

group 'com.project'
version '1.0-SNAPSHOT'

repositories {
  mavenCentral()
}

ext {
  junitVersion = '5.10.0'
}


tasks.withType(JavaCompile).configureEach {
  options.encoding = 'UTF-8'
}

application {
  mainModule = 'com.project.app'
  mainClass = 'com.project.app.DesktopKt'
}
kotlin {
  jvmToolchain(8)
}

tasks.register('customFatJar', Jar) {
  manifest {
    attributes 'Main-Class': 'com.project.app.DesktopKt'
  }
  archiveBaseName = 'app-jar'
  duplicatesStrategy = DuplicatesStrategy.EXCLUDE
  from {
    configurations.runtimeClasspath.collect {
      it.isDirectory() ? it : zipTree(it).matching {
        exclude 'META-INF/*.SF'
        exclude 'META-INF/*.DSA'
        exclude 'META-INF/*.RSA'
      }
    }
  }
  with jar
}


javafx {
  version = '11'
  modules = ['javafx.controls', 'javafx.fxml']
}

dependencies {
  implementation("org.telegram:telegrambots:6.7.0")
  implementation("org.seleniumhq.selenium:selenium-java:4.19.1")
  implementation("io.github.bonigarcia:webdrivermanager:5.8.0")
  implementation("com.squareup.retrofit2:retrofit:2.11.0")
  implementation("com.squareup.retrofit2:converter-gson:2.11.0")
  implementation("com.squareup.retrofit2:converter-simplexml:2.11.0")
  implementation("com.squareup.retrofit2:converter-scalars:2.11.0")
  implementation("org.jsoup:jsoup:1.17.2")
  testImplementation("org.junit.jupiter:junit-jupiter-api:${junitVersion}")
  testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junitVersion}")
}

test {
useJUnitPlatform()}

jlink {
  imageZip = project.file("${buildDir}/distributions/app-${javafx.platform.classifier}.zip")
  options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
  launcher {
    name = 'app'
  }
}

jlinkZip {
  group = 'distribution'
}